plugins {
    id 'dev.architectury.loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
}

allprojects {
    apply plugin: "java"
    apply plugin: "dev.architectury.loom"
    apply plugin: "maven-publish"

    version = "${project.mod_version}+${project.minecraft_base_version}"
    group = project.maven_group

    repositories {
        maven { url "https://maven.wispforest.io/releases/" }
        maven { url 'https://maven.neoforged.net/releases/' }
        mavenLocal()
    }

    dependencies {
        // To change the versions see the gradle.properties file
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings loom.layered {
            it.mappings("net.fabricmc:yarn:${rootProject.yarn_mappings}:v2")
            it.mappings("dev.architectury:yarn-mappings-patch-neoforge:1.21+build.4")
        }
//        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        // Fabric API. This is technically optional, but you probably want it anyway.
//        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

        neoForge "net.neoforged:neoforge:${project.neoforge_version}"
    }

    base {
        archivesName = "lavender-md-neo-${project.module_name}"
    }

    processResources {
        inputs.property "version", project.version

        filesMatching("META-INF/neoforge.mods.toml") {
            expand "version": project.version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
        it.options.release = 21
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    jar {
        from("LICENSE") {
            rename { "${it}_lavender_md" }
        }
    }

    def ENV = System.getenv()
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from project.components.java
                artifactId project.module_name
            }
        }

        repositories {
            maven {
                url ENV.MAVEN_URL
                credentials {
                    username ENV.MAVEN_USER
                    password ENV.MAVEN_PASSWORD
                }
            }
        }
    }
}